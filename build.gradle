// Apply Gradle plugins
plugins {
	id 'java'
	id 'eclipse'
	id 'idea'

    id 'com.github.hierynomus.license' version '0.11.0'
}

defaultTasks 'clean', 'licenseFormat', 'build'

// Project information
group = 'net.amigocraft.ttt'
version = '0.6.1-SNAPSHOT'

// Extended project information
ext.projectName = 'TTT'
ext.description = 'A Bukkit minigame based off the Garry\'s Mod gamemode Trouble In Terrorist Town.'
ext.inceptionYear = '2013'
ext.packaging = 'jar'
ext.versionSuffix = version.contains('SNAPSHOT') && System.getenv("BUILD_NUMBER") ? '-jnks' + System.getenv("BUILD_NUMBER") : ''

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
    bukkit = 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'
	mglib = 'net.amigocraft.mglib:mglib:0.3.1-SNAPSHOT'
}

// Project repositories
repositories {
	mavenCentral()
	maven { url 'http://hub.spigotmc.org/nexus/content/groups/public/' }
	maven { url 'http://repo.amigocraft.net/content/groups/public/' }
}

// Project dependencies
dependencies {
    compile bukkit
	compile mglib

	testCompile 'junit:junit:4.11'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
	testCompile 'org.mockito:mockito-core:1.10.8'
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

processResources {
  filter { String line -> line.replace('%VERSION%', versionSuffix) }
}

// License header formatting
license {
	header file('LICENSE')
	exclude '*.txt'
	sourceSets = project.sourceSets
	ignoreFailures false
	strictCheck true
	mapping {
		java = 'SLASHSTAR_STYLE'
	}
}
